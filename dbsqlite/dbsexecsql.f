      SUBROUTINE DBSEXECSQL (SQLCMD,IConn,LSCHED,IRC)
      IMPLICIT NONE
C
C DBSQLITE $Id$
C
C     PURPOSE: EXECUTES USER DEFINED QUERIES UPON OPENED
C              DATABASE CONNECTIONS
C     INPUT: SQLCMD   - SQL COMMAND QUERY TO BE EXECUTED
C            IConn - THE CONNECTION TO THE DBMS
C            LSCHED   - SPECIFIES WHETHER THIS IS COMING FROM THE EVENT
C                       MONITOR
COMMONS
C
C
      INCLUDE  'PRGPRM.F77'
C
C
      INCLUDE  'CONTRL.F77'
C
C
      INCLUDE  'OPCOM.F77'
C
C
      INCLUDE  'PLOT.F77'
C
C
      INCLUDE  'DBSCOM.F77'
C
C
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_ADDCOLIFABSENT
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_BIND_DOUBLE
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_BIND_INT
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_BIND_TEXT
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_CLOSE
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_COLCNT
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_COLDOUBLE
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_COLINT
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_COLISNULL
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_COLNAME
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_COLREAL
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_COLTEXT
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_COLTYPE
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_ERRMSG
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_EXEC
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_FINALIZE
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_OPEN
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_PREPARE
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_RESET
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_STEP
  !DEC$ ATTRIBUTES DLLIMPORT :: FSQL3_TABLEEXISTS
#if !(_WIN64)
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_ADDCOLIFABSENT' :: FSQL3_ADDCOLIFABSENT
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_BIND_DOUBLE'    :: FSQL3_BIND_DOUBLE
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_BIND_INT'       :: FSQL3_BIND_INT
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_BIND_TEXT'      :: FSQL3_BIND_TEXT
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_CLOSE'          :: FSQL3_CLOSE
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_COLCNT'         :: FSQL3_COLCNT
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_COLDOUBLE'      :: FSQL3_COLDOUBLE
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_COLINT'         :: FSQL3_COLINT
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_COLISNULL'      :: FSQL3_COLISNULL
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_COLNAME'        :: FSQL3_COLNAME
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_COLREAL'        :: FSQL3_COLREAL
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_COLTEXT'        :: FSQL3_COLTEXT
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_COLTYPE'        :: FSQL3_COLTYPE
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_ERRMSG'         :: FSQL3_ERRMSG
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_EXEC'           :: FSQL3_EXEC
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_FINALIZE'       :: FSQL3_FINALIZE
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_OPEN'           :: FSQL3_OPEN
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_PREPARE'        :: FSQL3_PREPARE
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_RESET'          :: FSQL3_RESET
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_STEP'           :: FSQL3_STEP
  !DEC$ ATTRIBUTES ALIAS:'_FSQL3_TABLEEXISTS'    :: FSQL3_TABLEEXISTS
#endif

COMMONS

      integer fsql3_prepare,fsql3_step,fsql3_colcnt,fsql3_colname,
     >        fsql3_colisnull,fsql3_finalize
      real fsql3_colreal
      CHARACTER*(*) SQLCMD
      INTEGER IConn,ColumnCount,MxNameLen,ColNumber,NameLen
      PARAMETER (MxNameLen=50)
      CHARACTER (LEN=MxNameLen) ColName
      INTEGER KODE,I,IOPKD
      LOGICAL LSCHED
      INTEGER iRet,IRC

      IRC = 1

C     MAKE SURE WE HAVE AN OPEN CONNECTION

      IF(IConn.EQ.-1) RETURN 

C     PARSE OUT AND REPLACE WITH USER DEFINED AND EVENT MONITOR VAR VALS
      CALL DBSPRSSQL(SQLCMD,LSCHED,KODE)
      IF(KODE.EQ.0) THEN
C       THERE WAS A PROBLEM IN PARSING THE SQL STATEMENT
        WRITE (JOSTND,110) TRIM(SQLCMD)
  110   FORMAT (/T12,'SQL STMT: ',A/
     &         '********   ERROR: SQLOUT/SQLIN PARSING FAILED. ')
        CALL RCDSET(2,.TRUE.)
        RETURN
      ENDIF
      
      iRet = fsql3_prepare(Iconn,trim(SQLCMD)//CHAR(0))  
      IF (iRet.NE.0) THEN 
        iRet = fsql3_finalize(Iconn)
        WRITE (JOSTND,120) TRIM(SQLCMD)
  120   FORMAT (/T12,'SQL STMT: ',A/
     &         '********   ERROR: SQLOUT/SQLIN PREPARE FAILED. ')
        CALL RCDSET(2,.TRUE.)
        RETURN 
      ENDIF
      DO WHILE(fsql3_step(Iconn)==1)
        ColumnCount = fsql3_colcnt(Iconn)
        IF(ColumnCount.GT.0) THEN
          DO ColNumber = 0,ColumnCount-1
            NameLen = fsql3_colname(Iconn,ColNumber,ColName,MxNameLen)
            ColName(NameLen+1:) = ""
            IOPKD = 0            
            DO I=1,ITST5
              IF (ColName(:8).EQ.CTSTV5(I)) THEN
                IOPKD = I
                EXIT
              ENDIF
            ENDDO
            IF (IOPKD.EQ.0) THEN
              IF(ITST5.LT.MXTST5) THEN
                ITST5 = ITST5+1
                CTSTV5(ITST5) = ColName(:8)
                LTSTV5(ITST5) = .FALSE.
                IOPKD = ITST5
              ENDIF
            ENDIF            
            IF(IOPKD.GT.0) THEN
               IF (fsql3_colisnull(Iconn,ColNumber) .eq. 1) THEN
                 TSTV5(IOPKD) = 0
                 LTSTV5(IOPKD) = .FALSE.
               ELSE 
                 TSTV5(IOPKD) = fsql3_colreal(Iconn,ColNumber,0)
                 LTSTV5(IOPKD) = .TRUE.
               ENDIF
            ENDIF
          ENDDO
        ENDIF
      ENDDO
      iRet = fsql3_finalize(Iconn)
      IRC = 0
      RETURN
      END
