      SUBROUTINE DUBSCR(ISPC,D,H,PRD,QMDPLT,CR,TPCT,TPCCF)
      IMPLICIT NONE
C----------
C NC $Id$
C----------
C  THIS SUBROUTINE CALCULATES CROWN RATIOS FOR TREES INSERTED BY
C  THE REGENERATION ESTABLISHMENT MODEL.  IT ALSO DUBS CROWN RATIOS
C  FOR TREES IN THE INVENTORY THAT ARE MISSING CROWN RATIO
C  MEASUREMENTS AND ARE LESS THAN 1.0 INCH DBH.  
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'PDEN.F77'
C
C
COMMONS
C----------
      LOGICAL DEBUG
      EXTERNAL RANN
      INTEGER ISPC
      REAL D,H,CR,FCR,SD,TPCT,TPCCF,BACHLO
      REAL BCR0(MAXSP),BCR1(MAXSP),BCR2(MAXSP),BCR3(MAXSP),
     & CRSD(MAXSP),BCR5(MAXSP),BCR6(MAXSP),
     & BCR8(MAXSP),BCR9(MAXSP),BCR10(MAXSP)
      REAL RDANUW,PRD,QMDPLT,HDR
C----------
      DATA BCR0/
     & -1.669490, -1.669490,  -.426688,  -.426688,  -.426688,
     & -0.426688, -1.669490,  -.426688,  -.426688, -1.669490,
     & -2.19723,   0.00000/
C
      DATA BCR1/
     &  -.209765,  -.209765,  -.093105,  -.093105,  -.093105,
     &  -.093105,  -.209765,  -.093105,  -.093105,  -.209765,
     &   .000000,    0.00000/
C
      DATA BCR2/
     &   .000000,   .000000,   .022409,   .022409,   .022409,
     &   .022409,   .000000,   .022409,   .022409,   .000000,
     &   .000000,   0.00000/
C
      DATA BCR3/
     &   .003359,   .003359,   .002633,   .002633,   .002633,
     &   .002633,   .003359,   .002633,   .002633,   .003359,
     &   .000000,   0.00000/
C
      DATA BCR5/
     &   .011032,   .011032,   .000000,   .000000,   .000000,
     &   .000000,   .011032,   .000000,   .000000,   .011032,
     &   .000000,   0.00000/
C
      DATA BCR6/
     &   .000000,   .000000,  -.045532,  -.045532,  -.045532,
     &  -.045532,   .000000,  -.045532,  -.045532,   .000000,
     &   .000000,   0.00000/
C
      DATA BCR8/
     &   .017727,   .017727,   .000000,   .000000,   .000000,
     &   .000000,   .017727,   .000000,   .000000,   .017727,
     &   .000000,   0.00000/
C
      DATA BCR9/
     &  -.000053,  -.000053,   .000022,   .000022,   .000022,
     &   .000022,  -.000053,   .000022,   .000022,  -.000053,
     &   .000000,   0.00000/
C
      DATA BCR10/
     &   .014098,   .014098,  -.013115,  -.013115,  -.013115,
     &  -.013115,   .014098,  -.013115,  -.013115,   .014098,
     &   .000000,   0.00000/
C
      DATA CRSD/
     &  .5000,.5000,.6957,.6957, .6957, .9310,
     &  .6124,.6957,.6957,.4942, 0.200, .1500/
C----------
C  DUMMY ARGUMENT NOT USED WARNING SUPPRESSION SECTION
C----------
      RDANUW = TPCT
C-----------
C  CHECK FOR DEBUG.
C-----------
      CALL DBCHK (DEBUG,'DUBSCR',6,ICYC)
      
C----------
C RW CR CALCULATION
C EXPECTED CROWN RATIO FOR RW IS A FUNCTION OF DBH, HEIGHT DIAMETER RATIO (H/D),
C RELATIVE DENSITY AND RELATIVE DIAMETER THE MODEL IS BASED ON THE
C LOGISTIC FUNCTION, AND RETURNS A VALUE BETWEEN ZERO AND ONE.
C----------

C  CALCULATE HEIGHT DIAMETER RATIO AND CONSTRAIN IF NEEDED
      HDR = (H*12)/D

C RW CR CALCULATION
      IF(ISPC .EQ. 12) THEN
        CR = -1.021064 + 0.309296*LOG(HDR) + 
     &        0.869720*PRD - 0.116274*(D/QMDPLT)

C RW DEBUG
        IF(DEBUG)WRITE(JOSTND,*)' IN DUBSCR - RW DEBUG',' D=',D,
     &  ' H=',H,' PRD=',PRD,' QMDPLT=',QMDPLT,' HDR=', HDR,' CR=',CR

C----------
C  EXPECTED CROWN RATIO IS A FUNCTION OF SPECIES, DBH, BASAL AREA, BAL,
C  AND PCCF.  THE MODEL IS BASED ON THE LOGISTIC FUNCTION,
C  AND RETURNS A VALUE BETWEEN ZERO AND ONE.
C----------

C ALL OTHER SPECIES CR CALCULATION
      ELSE
        CR = BCR0(ISPC)
     *   + BCR1(ISPC)*D
     *   + BCR2(ISPC)*H
     *   + BCR3(ISPC)*BA
     *   + BCR5(ISPC)*TPCCF
     *   + BCR6(ISPC)*(AVH/H)
     *   + BCR8(ISPC)*AVH
     *   + BCR9(ISPC)*(BA*TPCCF)
     *   + BCR10(ISPC)*RMAI
      ENDIF

C----------
C  A RANDOM ERROR IS ASSIGNED TO THE CROWN RATIO PREDICTION
C  PRIOR TO THE LOGISTIC TRANSFORMATION.  LINEAR REGRESSION
C  WAS USED TO FIT THE MODELS AND THE ELEMENTS OF CRSD
C  ARE THE STANDARD ERRORS FOR THE LINEARIZED MODELS BY SPECIES.
C----------
      SD=CRSD(ISPC)
   10 FCR=BACHLO(0.0,SD,RANN)
      IF(ABS(FCR).GT.SD) GO TO 10
      IF(ABS(CR+FCR).GE.86.)CR=86.
      CR=1.0/(1.0+EXP(CR+FCR))
      IF(CR .GT. .95) CR = .950
      IF(CR .LT. .05) CR=.05
      IF(DEBUG)WRITE(JOSTND,*)' IN DUBSCR','CR=',CR,' FCR=',FCR
C      IF(DEBUG)WRITE(JOSTND,600)ISPC,D,H,TBA,TPCCF,CR,FCR,RMAI
C 600  FORMAT(' IN DUBSCR, ISPC=',I2,' DBH=',F4.1,' H=',F5.1,
C    &       ' TBA=',F7.3,' TPCCF=',F8.4,' CR=',F4.3,
C    &       ' RAN ERR = ',F6.4,' RMAI= ',F9.4)

      RETURN
      END
